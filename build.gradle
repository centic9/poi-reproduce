apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

mainClassName = 'org.dstadler.poi.TestCloseBehavior'
version = '1.0'

jar {
    manifest {
        attributes 'Implementation-Title': 'Reproduce some behavior of Apache POI against different versions', 'Implementation-Version': archiveVersion
    }
}

sourceCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    // maven { url 'https://repository.apache.org/content/groups/staging/'}
}

// this is the version of the jar-files of Apache POI that are used for testing
if (!project.hasProperty('poiVersion')) {
	ext.poiVersion = '5.2.4'
}

if (!project.hasProperty('closeTest')) {
	ext.closeTest = 'stream'
}

dependencies {
    implementation 'org.apache.poi:poi:' + poiVersion
    implementation 'org.apache.poi:poi-ooxml:' + poiVersion
    /*implementation files('/tmp/poi-bin-5.2.5/lib/commons-collections4-4.4.jar')
    implementation files('/tmp/poi-bin-5.2.5/lib/commons-math3-3.6.1.jar')
    implementation files('/tmp/poi-bin-5.2.5/ooxml-lib/commons-compress-1.21.jar')
    implementation files('/tmp/poi-bin-5.2.5/ooxml-lib/xmlbeans-5.0.2.jar')
    implementation files('/tmp/poi-bin-5.2.5/poi-5.2.5.jar')
    implementation files('/tmp/poi-bin-5.2.5/poi-ooxml-5.2.5.jar')
    implementation files('/tmp/poi-bin-5.2.5/poi-ooxml-lite-5.2.5.jar')*/

	implementation 'org.apache.logging.log4j:log4j-core:2.20.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

test {
    useJUnitPlatform()
}

wrapper {
    gradleVersion = '7.6.1'
}

jacoco {
    toolVersion = '0.8.10'
}

tasks.register('runClose', JavaExec) {
	dependsOn compileJava
	description = 'Performs a check with file-leak-detector and the test application'

	jvmArgs = [
			'-Xmx512m',
			'-javaagent:/opt/file-leak-detector/target/file-leak-detector-1.18-SNAPSHOT-jar-with-dependencies.jar=http=0,strong,excludes=/opt/apache/poi/git-svn/file-leak-detector.exclude,dumpatshutdown',
			'-XX:+HeapDumpOnOutOfMemoryError', '-XX:HeapDumpPath=/tmp'
	]

	mainClass = 'org.dstadler.poi.TestCloseBehavior'
	args = [closeTest]
	classpath = sourceSets.main.runtimeClasspath
}
